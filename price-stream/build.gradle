plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.application)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

group = 'com.price'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation project(':price-common')
    implementation project(':modules:price-db-clickhouse')
    runtimeOnly project(':modules:price-source-binance')

    implementation libs.spring.boot.starter
    implementation libs.spring.boot.starter.log4j2

    implementation libs.disruptor
    implementation libs.commons.lang3
    implementation libs.commons.io
    implementation libs.json
    implementation libs.jackson.databind

    // Jetty
    implementation libs.bundles.jetty

    // Netty
    implementation libs.netty.all

    // Logging
    implementation libs.bundles.log4j

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // Testing
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.jetty.client
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.price.stream.StreamServer'
}

jar {
    enabled = false
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

tasks.register('fatJar', Jar) {
    dependsOn ':price-common:jar', ':modules:price-db-clickhouse:jar', ':modules:price-source-binance:jar'
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': application.mainClass)
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}
