plugins {
    id 'java'
    id 'application'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.binance:binance-connector-java:3.4.1'
    implementation 'com.lmax:disruptor:4.0.0'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'org.json:json:20231013'

    // ClickHouse
    implementation 'com.clickhouse:clickhouse-jdbc:0.6.5'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    implementation 'org.lz4:lz4-java:1.8.0'

    // Jetty
    implementation 'org.eclipse.jetty:jetty-server:11.0.24'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.24'

    // Logging
    implementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
    testImplementation 'org.eclipse.jetty:jetty-client:11.0.24'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.price.Server'
}

jar {
    enabled = false
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': application.mainClass)
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}
