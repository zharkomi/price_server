plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

group = 'com.price'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation project(':price-common')
    implementation project(':modules:price-db-clickhouse')

    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.log4j2

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation libs.spring.boot.starter.test
}

tasks.named('test') {
    useJUnitPlatform()
}

application {
    mainClass = 'com.price.query.QueryServer'
}

tasks.register('fatJar', Jar) {
    dependsOn ':price-common:jar', ':modules:price-db-clickhouse:jar'
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': application.mainClass)
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}